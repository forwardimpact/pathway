# Basecamp — build, install, and manage the scheduler

app_name       := "fit-basecamp"
launcher       := "Basecamp"
version        := `grep '"version"' package.json | head -1 | sed 's/.*: *"//;s/".*//;s/ //g'`
plist_name     := "com.forwardimpact.basecamp"
dist_dir       := "dist"

# Show available recipes
[doc("List all available recipes")]
default:
    @just --list

# ─── Build ────────────────────────────────────────────────────────────────────

# Compile standalone Deno scheduler binary
build-scheduler:
    @mkdir -p "{{ dist_dir }}"
    deno compile --allow-all --no-check \
        --output "{{ dist_dir }}/{{ app_name }}" src/basecamp.js

# Compile Swift app launcher (includes status menu UI)
build-launcher:
    @mkdir -p "{{ dist_dir }}"
    @rm -rf macos/Basecamp/.build
    swift build -c release --package-path macos/Basecamp
    @cp macos/Basecamp/.build/release/{{ launcher }} "{{ dist_dir }}/{{ launcher }}"
    @rm -rf macos/Basecamp/.build

# Compile everything (scheduler first so launcher binary is not embedded)
build: build-scheduler build-launcher

# Assemble Basecamp.app bundle
build-app: build
    bash pkg/macos/build-app.sh

# Build macOS installer package (.pkg)
pkg: build-app
    bash pkg/macos/build-pkg.sh "{{ dist_dir }}" "{{ version }}"

# Clean build artifacts
clean:
    rm -rf "{{ dist_dir }}" 2>/dev/null; sudo rm -rf "{{ dist_dir }}" 2>/dev/null; true

# ─── Setup ───────────────────────────────────────────────────────────────────

# Uninstall (remove app; user data preserved)
uninstall:
    bash pkg/macos/uninstall.sh

# ─── Scheduler ────────────────────────────────────────────────────────────────

# Run due tasks once and exit
run:
    deno run --allow-all src/basecamp.js

# Run scheduler daemon (poll every 60s)
daemon:
    deno run --allow-all src/basecamp.js --daemon

# Run a specific task immediately
run-task task:
    deno run --allow-all src/basecamp.js --run "{{ task }}"

# Show knowledge bases and task status
status:
    deno run --allow-all src/basecamp.js --status

# Initialize a new knowledge base at the given path
init path:
    deno run --allow-all src/basecamp.js --init "{{ path }}"
