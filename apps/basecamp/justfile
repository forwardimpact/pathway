# Basecamp — build, install, and manage the scheduler

app_name       := "fit-basecamp"
status_menu    := "BasecampStatus"
version        := `grep '"version"' package.json | head -1 | sed 's/.*: *"//;s/".*//;s/ //g'`
plist_name     := "com.fit-basecamp.scheduler"
dist_dir       := "dist"
status_menu_bin := dist_dir / status_menu

# Show available recipes
[doc("List all available recipes")]
default:
    @just --list

# ─── Build ────────────────────────────────────────────────────────────────────

# Compile the Swift status menu binary
build-status-menu:
    @mkdir -p "{{ dist_dir }}"
    @rm -rf StatusMenu/.build
    swift build -c release --package-path StatusMenu
    @cp StatusMenu/.build/release/{{ status_menu }} "{{ status_menu_bin }}"
    @rm -rf StatusMenu/.build

# Compile standalone Deno binary
build-daemon:
    ./pkg/macos/compile.sh "{{ dist_dir }}" "{{ app_name }}"

# Compile everything (Deno first so status menu binary is not embedded)
build: build-daemon build-status-menu

# Build macOS installer package (.pkg)
pkg: build
    ./pkg/macos/build-pkg.sh "{{ dist_dir }}" "{{ app_name }}" "{{ version }}" "{{ status_menu_bin }}"

# Clean build artifacts
clean:
    rm -rf "{{ dist_dir }}"

# ─── Setup ───────────────────────────────────────────────────────────────────

# Initialize config and default KB at ~/Documents/Personal/ (dev)
setup:
    ./scripts/init.sh

# Uninstall pkg (remove binary and LaunchAgent; user data preserved)
uninstall:
    ./pkg/macos/uninstall.sh "{{ app_name }}" "{{ plist_name }}"

# ─── Scheduler ────────────────────────────────────────────────────────────────

# Run due tasks once and exit
run:
    deno run --allow-all basecamp.js

# Run scheduler daemon (poll every 60s)
daemon:
    deno run --allow-all basecamp.js --daemon

# Run a specific task immediately
run-task task:
    deno run --allow-all basecamp.js --run "{{ task }}"

# Show knowledge bases and task status
status:
    deno run --allow-all basecamp.js --status

# Initialize a new knowledge base at the given path
init path:
    deno run --allow-all basecamp.js --init "{{ path }}"
